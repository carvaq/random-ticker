apply plugin: 'com.android.application'
apply from: 'signing_config.gradle'

def getGitVersionName = { ->
    String tag = ("git describe --abbrev=0 --tags").execute().text.trim()
    if (tag.contains('/') && tag.contains('.')) {
        return tag.substring(tag.indexOf('/') + 1, tag.length())
    } else {
        throw new Exception("tag has no slashes or a period [${tag}]")
    }
}

def parseVersionCode = { versionName ->
    def versionCode = (int) (Double.parseDouble(versionName) * 1000)
    println "Version name: ${versionName}  Version code: ${versionCode}"
    return versionCode
}

ext {
    gitVersionName = getGitVersionName()
    gitVersionCode = parseVersionCode(gitVersionName)
}

android {
    compileSdkVersion 26
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.cvv.fanstaticapps.randomticker"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [toothpick_registry_package_name: 'com.cvv.fanstaticapps.randomticker']
            }
        }

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    annotationProcessor 'org.xelevra.libs:prefdata-processor:2.2'
    annotationProcessor 'com.github.kobakei.grenade:processor:1.1.0'
    annotationProcessor 'com.github.stephanenicolas.toothpick:toothpick-compiler:1.0.8'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'pl.bclogic:pulsator4droid:1.0.3'
    implementation 'com.shawnlin:number-picker:2.4.4'
    implementation 'com.github.stephanenicolas.toothpick:toothpick-runtime:1.0.8'
    implementation 'com.github.stephanenicolas.toothpick:smoothie:1.0.8'
    implementation 'com.github.kobakei.grenade:library:1.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.github.tarek360.RichPath:animator:0.0.9'
    provided 'org.xelevra.libs:prefdata-annotations:2.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
}