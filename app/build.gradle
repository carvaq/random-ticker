apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.cookpad.android.licensetools'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: 'release_config.gradle'

def getGitVersionName = { ->
	String tag = ("git describe --abbrev=0 --tags").execute().text.trim()
	if (tag.contains('/') && tag.contains('.')) {
		return tag.substring(tag.indexOf('/') + 1, tag.length())
	} else {
		throw new Exception("tag has no slashes or a period [${tag}]")
	}
}

def parseVersionCode = {
	String versionName = gitVersionName
	def versionCodeParts = versionName.tokenize('.')
	int decimals = Integer.parseInt(versionCodeParts.get(1))
	String formattedVersionCode = String.format("%s%02d00", versionCodeParts.get(0), decimals)
	def versionCode = Integer.parseInt(formattedVersionCode)
	println "Version name: ${versionName}  Version code: ${versionCode}"
	return versionCode
}

ext {
	gitVersionName = getGitVersionName()
	gitVersionCode = parseVersionCode(gitVersionName)
}

kapt {
	generateStubs = false
}

android {
	compileSdkVersion 'android-P'
	buildToolsVersion '28.0.0 rc2'
	defaultConfig {
		applicationId "com.cvv.fanstaticapps.randomticker"
		minSdkVersion 21
		targetSdkVersion 27
		versionCode gitVersionCode
		versionName gitVersionName
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}
	buildTypes {
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
	}

	testOptions {
		unitTests {
			includeAndroidResources = true
		}
	}
}

def daggerVersion = '2.13'

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
	implementation 'androidx.core:core-ktx:1.0.0-alpha1'
	implementation 'com.google.android.material:material:1.0.0-alpha1'
	implementation 'androidx.cardview:cardview:1.0.0-alpha1'
	implementation 'pl.bclogic:pulsator4droid:1.0.3'
	implementation 'com.github.kobakei.grenade:library:1.1.0'
	implementation "com.google.dagger:dagger:$daggerVersion"
	implementation "com.google.dagger:dagger-android:$daggerVersion"
	implementation "com.google.dagger:dagger-android-support:$daggerVersion"

	kapt "com.google.dagger:dagger-compiler:$daggerVersion"
	kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
	kapt 'com.github.kobakei.grenade:processor:1.1.0'

	testImplementation 'org.mockito:mockito-core:2.13.0'
	testImplementation 'junit:junit:4.12'
	testImplementation 'org.robolectric:robolectric:3.6.1'
	testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"

	androidTestImplementation 'androidx.test.uiautomator:uiautomator-v18:2.2.0-alpha1'
	androidTestImplementation 'androidx.test:rules:1.1.0-alpha3'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha3'
}